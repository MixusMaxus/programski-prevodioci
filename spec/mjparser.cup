package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:
	
	boolean errorDetected = false;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}

terminal PROGRAM, LEFTBRACE, RIGHTBRACE, NAMESPACE, LEFTBRACKET, RIGHTBRACKET, LEFTPAREN, RIGHTPAREN, SEMICOLON, COMMA, STATIC ;

terminal CONST, VOID, NEW, CLASS, EXTENDS, DOT, ARROW ;

terminal EQUAL, ISEQUAL, NOTEQUAL, GREATER, GREATEREQUAL, LESS, LESSEQUAL, PLUS, MINUS, MULTIPLY, DIVIDE, MODUO, DOUBLECOLON ;

terminal IF, ELSE, BREAK, CONTINUE, RETURN, READ, PRINT, FOR, INCREMENT, DECREMENT, AND, OR, COLON ;

nonterminal Program, NamespaceList, DeclarationList, Namespace, Declaration, VarDecl, Type, Label ;

nonterminal Constant, ConstDecl, ConstantList, ConstantDeclaration, VarList, Variable, StaticInitializer, StatementList ;

nonterminal MethodDeclList, MethodDecl, MethodReturnType, MethodParamList, FormParsList, VarDeclList, OneParamMethod, FactorNewType ;

nonterminal Statement, DesignatorStatement, ForDesignatorStatement, InnerForDesignatorStatement, InnerForDesignatorStatementList, ForCondFact, MethodName ;

nonterminal Designator, Expr, InsideFor, FirstDesignator, SecondDesignator, DesignatorList, OneDesignator, IdentExpressList, IdentExpressListElem, ArrayBrackets ;

nonterminal DesignatorIdentificator, NamespaceName, Assignop, Relop, Addop, Mulop, ActParsList, ActPars, Condition, CondFact, CondTerm, Term, AddopTermList, Factor, FactorMulopList, ProgramName ;

nonterminal StatementElseStatement, StatementReturn, StatementPrint, CondFactExprRelopExpr, FactorDesignatorActParsList ;

terminal String IDENTIFICATOR;
terminal Character CHARCONSTANT;
terminal Integer NUMCONSTANT;
terminal Boolean BOOLCONSTANT;

precedence left ELSE;

Program ::= (Program) PROGRAM ProgramName NamespaceList DeclarationList LEFTBRACE MethodDeclList RIGHTBRACE ;

ProgramName ::= (ProgramName) IDENTIFICATOR ;

NamespaceList ::= (MultipleNamespaces) NamespaceList Namespace
				   |
				  (NoNamespaces) /*epsilon*/
				   ;

Namespace ::= (Namespace) NAMESPACE NamespaceName LEFTBRACE DeclarationList LEFTBRACE MethodDeclList RIGHTBRACE RIGHTBRACE ;

NamespaceName ::= (NamespaceName) IDENTIFICATOR ;

DeclarationList ::= (MultipleDeclarations) DeclarationList Declaration
				    |
					(NoDeclarations)/*epsilon*/
				    ;

Declaration ::= (DeclarationConstDecl) ConstDecl 
				|
				(DeclarationVarDecl) VarDecl 
				/*|
				 ClassDecl*/
				;
				
ConstDecl ::= (ConstDecl) CONST Type ConstantList SEMICOLON ;

ConstantList ::= (MultipleConstants) ConstantList COMMA ConstantDeclaration 
				|
				(OneConstant) ConstantDeclaration
				;

ConstantDeclaration ::= (ConstantDeclaration) IDENTIFICATOR EQUAL Constant ;

Constant ::= (NumberConstant) NUMCONSTANT 
			|
			(CharacterConstant) CHARCONSTANT  
			|
			(BooleanConstant) BOOLCONSTANT 
			;
			
VarDecl ::= (VarDecl) Type VarList SEMICOLON ;

VarList ::= (MultipleVariables) VarList COMMA Variable 
			|
			(OneVariable) Variable
			;

Variable ::= (VariableName) IDENTIFICATOR ArrayBrackets
				|
				error SEMICOLON:l {: parser.report_error("Greska kod deklaracije promenljive na liniji " + lleft, null); :}
				;

ArrayBrackets ::= (HasArrayBrackets) LEFTBRACKET RIGHTBRACKET
					|
				  (NoArrayBrackets) /*epsilon*/
				  ;

StaticInitializer ::= (StaticInitializer) STATIC LEFTBRACE StatementList RIGHTBRACE ;

MethodDeclList ::= (MultipleMethodDeclarations) MethodDeclList MethodDecl
					|
					(NoMethodDeclarations) /*epsilon*/
					;

MethodDecl ::= (MethodDecl) MethodReturnType MethodName LEFTPAREN MethodParamList RIGHTPAREN VarDeclList LEFTBRACE StatementList RIGHTBRACE ;

MethodName ::= (MethodName) IDENTIFICATOR ;

MethodReturnType ::= (MethodReturnTypeType)Type 
					  | 
					 (MethodReturnTypeVoid)VOID ;

MethodParamList ::= (MultipleMethodParamethers) FormParsList 
					|
					(NoMethodParamethers) /*epsilon*/
					;


FormParsList ::= (MultipleFormParamethers) FormParsList COMMA OneParamMethod
				|
				(OneFormParamether) OneParamMethod
				;

OneParamMethod ::= (OneParametherMethod) Type IDENTIFICATOR ArrayBrackets
					|
				   (OneParametherMethodError) error RIGHTPAREN:l   {: parser.report_error("Greska kod deklaracije formalnog parametra funkcije na liniji. " + lleft, null); :} 
				   ;

VarDeclList ::= (MultipleVarDeclarations) VarDeclList VarDecl
				|
				(NoVarDeclarations) /*epsilon*/
				;
				
Type ::= (NormalanType) IDENTIFICATOR
		 |
		 (NenormalanType) IDENTIFICATOR DOUBLECOLON IDENTIFICATOR;

Statement ::= (StatementDesignator) DesignatorStatement SEMICOLON
				|
				(StatementIfElse) IF LEFTPAREN Condition RIGHTPAREN Statement StatementElseStatement
				|
				(StatementBreak) BREAK SEMICOLON
				|
				(StatementContinue) CONTINUE SEMICOLON
				|
				(ReturnStatement) RETURN StatementReturn
				|
				(StatementRead) READ LEFTPAREN Designator RIGHTPAREN SEMICOLON
				|
				(PrintStatement) PRINT LEFTPAREN Expr StatementPrint
				|
				(StatementFor) FOR LEFTPAREN InsideFor RIGHTPAREN Statement
				|
				(MultipleStatements) LEFTBRACE StatementList RIGHTBRACE
			 	;

StatementElseStatement ::= (ElseStatement) ELSE Statement
							|
							(NoElseStatement) /*epsilon*/
							;

StatementReturn ::= (StmtReturnSemi) SEMICOLON
					|
					(StmtReturnExpr) Expr SEMICOLON
					;

StatementPrint ::= (StmtPrintBasic) RIGHTPAREN SEMICOLON
					|
				   (StmtPrintComplex) COMMA NUMCONSTANT RIGHTPAREN SEMICOLON
				   ;

InsideFor ::= (InsideFor) ForDesignatorStatement SEMICOLON ForCondFact SEMICOLON ForDesignatorStatement ;

ForCondFact ::= (ForCndFact) CondFact 
				|
				(NoForCndFact)/*epsilon*/
				;
				
ForDesignatorStatement ::= (ForDesignatorStmtInner) InnerForDesignatorStatement
							|
							(NoForDesignatorStmt)/*epsilon*/
							;

InnerForDesignatorStatement ::= (InnerForDesignatorStatement) DesignatorStatement InnerForDesignatorStatementList ;

InnerForDesignatorStatementList ::= (MultipleInnerForDesignatorStatementLists) InnerForDesignatorStatementList COMMA DesignatorStatement
									|
									(NoInnerForDesignatorStatementLists) /*epsilon*/
									;

StatementList ::= (MultipleStatementsLists) StatementList Statement
				|
				(NoStatementsLists) /*epsilon */
			 	;

DesignatorStatement ::= (DesignatorStmtFirst) Designator FirstDesignator
						|
						(DesignatorStmtSecond) SecondDesignator EQUAL Designator
						;

FirstDesignator ::= (FirstDesignatorAssignop) Assignop Expr
					|
					(FirstDesignatorActPars)LEFTPAREN ActParsList RIGHTPAREN
					|
					(FirstDesignatorInc) INCREMENT
					|
					(FirstDesignatorDec) DECREMENT
					;
				
SecondDesignator ::= (SecondDesignator) LEFTBRACKET DesignatorList MULTIPLY Designator RIGHTBRACKET ;

DesignatorList ::= (MultipleDesignatorLists) DesignatorList COMMA OneDesignator
					|
					(OneDsg) OneDesignator
					;
					
OneDesignator ::= (OneDesignatorDsg) Designator 
					|
					(NoDesignator) /*epsilon*/
					;
					
ActPars ::= (MultipleActPars) ActPars COMMA Expr
			|
			(ActParsExpr) Expr
			;
			
Condition ::= (ConditionOr) Condition OR CondTerm
				|
				(ConditionCondTerm) CondTerm
				|
				(ConditionError) error RIGHTPAREN:l   {: parser.report_error("Greska kod logickog izraza unutar if konstrukcije na liniji. " + lleft, null); :} 
				;
				
CondTerm ::= (CondTermAnd) CondTerm AND CondFact
			|
			(CondTermFact) CondFact
			;

CondFact ::= (CondFact) Expr CondFactExprRelopExpr ;
			
			
CondFactExprRelopExpr ::= (CondFactComplex) Relop Expr
							|
							(CondFactSimple) /*epsilon*/
							;
			
Expr ::= (ExprTerm) Term AddopTermList
		|
		(ExprMinus) MINUS Term AddopTermList
		;

AddopTermList ::= (MultipleAddopTerms) AddopTermList Addop Term
				|
				(NoAddopTermLists)/*epsilon*/
				;
				
Term ::= (Term) Factor FactorMulopList ;

FactorMulopList ::= (MultipleFactorMulopLists) FactorMulopList Mulop Factor
					|
					(NoFactorMulopList) /*epsilon*/
					;

Factor ::= (FactorDesignator) Designator FactorDesignatorActParsList
			|
			(FactorNumCnst) NUMCONSTANT
			|
			(FactorCharCnst)CHARCONSTANT
			|
			(FactorBoolCnst)BOOLCONSTANT
			|
			(FactorNew) NEW Type FactorNewType
			|
			(FactorExpr) LEFTPAREN Expr RIGHTPAREN
			;
			
FactorDesignatorActParsList ::= (ParenPars) LEFTPAREN ActParsList RIGHTPAREN
								|
								(NoParenPars) /*epsilon*/
								;
	
FactorNewType ::= (FactorNewTypeBracket) LEFTBRACKET Expr RIGHTBRACKET
				  |
				 (FactorNewTypeParen) LEFTPAREN ActPars RIGHTPAREN
				  ;
		
ActParsList ::= (ActParsComplex) ActPars
				|
				(NoActPars) /*epsilon*/
				;

Designator ::= (Designator) IDENTIFICATOR DesignatorIdentificator;
			
DesignatorIdentificator ::= (DesignatorIdentificatorComplex) DOUBLECOLON IDENTIFICATOR IdentExpressList
							|
							(DesignatorIdentificatorSimple) IdentExpressList
							;

IdentExpressList ::= (MultiplesIdentExpresses) IdentExpressList IdentExpressListElem
					|
					(NoIdentExpresses)/*epsilon*/
					;

IdentExpressListElem ::= (IdentExpressListElemIdent) DOT IDENTIFICATOR
						 |
						 (IdentExpressListElemExpr) LEFTBRACKET Expr RIGHTBRACKET
						 ;

Label ::= (Label) IDENTIFICATOR ;

Assignop ::= (AssignopEqual) EQUAL
			|
			(AssignopError)error SEMICOLON:l {: parser.report_error("Greska kod dodele vrednosti na liniji " + lleft, null); :}
			;

Relop ::=  (RelopEqual)ISEQUAL 
			| 
			(RelopNotEqual)NOTEQUAL 
			| 
			(RelopGrt)GREATER 
			| 
			(RelopGrtEqual)GREATEREQUAL 
			| 
			(RelopLess)LESS 
			| 
			(RelopLessEqual)LESSEQUAL 
			;

Addop ::= (AddopPlus)PLUS 
		| 
		(AddopMinus)MINUS 
		;

Mulop ::= (MulopMul) MULTIPLY 
		| 
		(MulopDiv)DIVIDE 
		| 
		(MulopMod)MODUO 
		;